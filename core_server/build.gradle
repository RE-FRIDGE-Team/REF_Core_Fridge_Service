plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.refridge'
version = '0.0.1-SNAPSHOT'
description = 'core_server'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

// Q클래스 생성 디렉토리 설정
def querydslDir = "$buildDir/generated/querydsl"

// 명시적으로 소스 디렉토리 지정 (선택사항)
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', querydslDir]
        }
    }
    test {
        groovy {
            srcDirs = ['src/test/groovy']
        }
    }
}

// Q클래스 생성 태스크
tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory = file(querydslDir)
}

// clean 시 Q클래스 삭제
clean {
    delete file(querydslDir)
}

idea {
    module {
        sourceDirs += file('build/generated/querydsl')
        generatedSourceDirs += file('build/generated/querydsl')
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'com.mysql:mysql-connector-j'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.apache.groovy:groovy'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    annotationProcessor 'org.projectlombok:lombok'

    // QueryDSL 의존성 - 활발히 유지보수되는 OpenFeign의 QueryDSL fork를 사용하는 것이 권장되며, 이 fork의 6.10.1 이상 버전에서 취약점이 패치
    implementation 'io.github.openfeign.querydsl:querydsl-jpa:7.1'
    annotationProcessor 'io.github.openfeign.querydsl:querydsl-apt:7.1'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'


    // Spock Framework
    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'


    // H2 Database (MySQL 호환 모드)
    implementation 'com.h2database:h2'

    // Groovy (Spock에 필요)
    testImplementation 'org.apache.groovy:groovy:4.0.15'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-redis-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
